#version 450

const float M_PI = 3.1415926535897932384626433832795;
const float M_SQRT1_2 = 0.70710678118654752440084436210485;

layout (local_size_x_id = 1, local_size_y_id = 2, local_size_z_id = 3) in;
layout (constant_id = 4) const uint fft_dim = 2048;

layout (constant_id = 5) const bool inverse = false;
layout (constant_id = 6) const bool zeropad_0 = false;
layout (constant_id = 7) const bool zeropad_1 = false;
layout (constant_id = 8) const uint inputStride_0 = 1;
layout (constant_id = 9) const uint inputStride_1 = 1;
layout (constant_id = 10) const uint inputStride_2 = 1;
layout (constant_id = 11) const uint inputStride_3 = 1;
layout (constant_id = 12) const uint inputStride_4 = 1;
layout (constant_id = 13) const uint outputStride_0 = 1;
layout (constant_id = 14) const uint outputStride_1 = 1;
layout (constant_id = 15) const uint outputStride_2 = 1;
layout (constant_id = 16) const uint outputStride_3 = 1;
layout (constant_id = 17) const uint outputStride_4 = 1;
layout (constant_id = 18) const uint radixStride_0 = 1;
layout (constant_id = 19) const uint radixStride_1 = 1;
layout (constant_id = 20) const uint radixStride_2 = 1;
layout (constant_id = 21) const uint numStages = 1;
layout (constant_id = 22) const uint stageRadix_0 = 8;
layout (constant_id = 23) const uint stageRadix_1 = 8;
layout (constant_id = 24) const uint ratio_0 = 8;
layout (constant_id = 25) const uint ratio_1 = 8;
layout (constant_id = 26) const bool ratioDirection_0 = false;
layout (constant_id = 27) const bool ratioDirection_1 = true;
layout (constant_id = 28) const uint inputOffset = 0;
layout (constant_id = 29) const uint outputOffset = 0;

layout(push_constant) uniform PushConsts
{
	uint coordinate;
	uint batchID;
} consts;


layout(std430, binding = 0) buffer Data {
    vec2 inputs[];
};

layout(std430, binding = 1) buffer Data2 {
    vec2 outputs[];
};
uint indexInput(uint index_x, uint index_y) {
    return inputOffset+index_x * inputStride_0 + index_y * inputStride_1 + gl_GlobalInvocationID.z * inputStride_2 + consts.coordinate * inputStride_3  + consts.batchID * inputStride_4;
}
uint indexOutput(uint index_x, uint index_y) {
    return outputOffset+index_x * outputStride_0 + index_y * outputStride_1 + gl_GlobalInvocationID.z * outputStride_2 + consts.coordinate * outputStride_3 + consts.batchID * outputStride_4;
}
uint positionShuffle(uint pos, uint ratio, bool ratioDirection ) {
	return (((pos)/(ratio))+((pos)%(ratio))*(fft_dim));
}
void radix2(inout vec2 values[2], vec2 w) {
    vec2 temp;
	temp.x=values[1].x*w.x-values[1].y*w.y;
	temp.y=values[1].y*w.x+values[1].x*w.y;
	values[1]=values[0]-temp;
	values[0]=values[0]+temp;
}

void radix4(inout vec2 values[4],inout vec2 w) {
	
	//DIF 1st stage with double angle
	vec2 temp;
	temp.x=values[2].x*w.x-values[2].y*w.y;
	temp.y=values[2].y*w.x+values[2].x*w.y;
	values[2]=values[0]-temp;
	values[0]=values[0]+temp;
	
	temp.x=values[3].x*w.x-values[3].y*w.y;
	temp.y=values[3].y*w.x+values[3].x*w.y;
	values[3]=values[1]-temp;
	values[1]=values[1]+temp;
	
	//DIF 2nd stage with half angle
	w = normalize(w + vec2(1.0, 0.0));
	
	temp.x=values[1].x*w.x-values[1].y*w.y;
	temp.y=values[1].y*w.x+values[1].x*w.y;
	values[1]=values[0]-temp;
	values[0]=values[0]+temp;
	
	w = (inverse) ? vec2(w.y, -w.x) : vec2(-w.y, w.x);

	temp.x=values[3].x*w.x-values[3].y*w.y;
	temp.y=values[3].y*w.x+values[3].x*w.y;
	values[3]=values[2]-temp;
	values[2]=values[2]+temp;
	
	temp = values[1];
	values[1]=values[2];
	values[2]=temp;
}

void radix8(inout vec2 values[8], inout vec2 w) {
	//DIF 1st stage with quadruple angle
	
	vec2 temp; 
	temp.x=values[4].x*w.x-values[4].y*w.y;
	temp.y=values[4].y*w.x+values[4].x*w.y;
	values[4]=values[0]-temp;
	values[0]=values[0]+temp;
	
	temp.x=values[5].x*w.x-values[5].y*w.y;
	temp.y=values[5].y*w.x+values[5].x*w.y;
	values[5]=values[1]-temp;
	values[1]=values[1]+temp;
	
	temp.x=values[6].x*w.x-values[6].y*w.y;
	temp.y=values[6].y*w.x+values[6].x*w.y;
	values[6]=values[2]-temp;
	values[2]=values[2]+temp;
	
	temp.x=values[7].x*w.x-values[7].y*w.y;
	temp.y=values[7].y*w.x+values[7].x*w.y;
	values[7]=values[3]-temp;
	values[3]=values[3]+temp;
	
	//DIF 2nd stage with double angle
	w = normalize(w + vec2(1.0, 0.0));
	
	temp.x=values[2].x*w.x-values[2].y*w.y;
	temp.y=values[2].y*w.x+values[2].x*w.y;
	values[2]=values[0]-temp;
	values[0]=values[0]+temp;
	
	temp.x=values[3].x*w.x-values[3].y*w.y;
	temp.y=values[3].y*w.x+values[3].x*w.y;
	values[3]=values[1]-temp;
	values[1]=values[1]+temp;
	
	vec2 iw = (inverse) ? vec2(w.y, -w.x) : vec2(-w.y, w.x);
    
	temp.x=values[6].x*iw.x-values[6].y*iw.y;
	temp.y=values[6].y*iw.x+values[6].x*iw.y;
	values[6]=values[4]-temp;
	values[4]=values[4]+temp;
	
	temp.x=values[7].x*iw.x-values[7].y*iw.y;
	temp.y=values[7].y*iw.x+values[7].x*iw.y;
	values[7]=values[5]-temp;
	values[5]=values[5]+temp;
	
	//DIF 3rd stage with angle
	w = normalize(w + vec2(1.0, 0.0));
	
	temp.x=values[1].x*w.x-values[1].y*w.y;
	temp.y=values[1].y*w.x+values[1].x*w.y;
	values[1]=values[0]-temp;
	values[0]=values[0]+temp;
	
	iw = (inverse) ? vec2(w.y, -w.x) : vec2(-w.y, w.x);
	
	temp.x=values[3].x*iw.x-values[3].y*iw.y;
	temp.y=values[3].y*iw.x+values[3].x*iw.y;
	values[3]=values[2]-temp;
	values[2]=values[2]+temp;
	
	iw.x=(inverse) ? w.x*M_SQRT1_2+w.y*M_SQRT1_2 : w.x*M_SQRT1_2-w.y*M_SQRT1_2;
	iw.y=(inverse) ? w.y*M_SQRT1_2-w.x*M_SQRT1_2 : w.y*M_SQRT1_2+w.x*M_SQRT1_2;
   
	temp.x=values[5].x*iw.x-values[5].y*iw.y;
	temp.y=values[5].y*iw.x+values[5].x*iw.y;
	values[5]=values[4]-temp;
	values[4]=values[4]+temp;
	
	w = (inverse) ? vec2(iw.y, -iw.x) : vec2(-iw.y, iw.x);
	
	temp.x=values[7].x*w.x-values[7].y*w.y;
	temp.y=values[7].y*w.x+values[7].x*w.y;
	values[7]=values[6]-temp;
	values[6]=values[6]+temp;
	
	temp = values[1];
	values[1]=values[4];
	values[4]=temp;
	
	temp = values[3];
	values[3]=values[6];
	values[6]=temp;

}

const uint max_shared_vec2=4096;
const uint last_ratio =  2;//  reg mem/shared mem
const uint tempSize = fft_dim/gl_WorkGroupSize.x;
shared vec2 sdata[max_shared_vec2];// half real half imag

void main() {
	vec2 temp0[8];	
	vec2 temp1[8];
	
	if (zeropad_0){
		if (ratioDirection_0){
			if (gl_WorkGroupID.y < gl_NumWorkGroups.y-1){
				vec2 sort0[8];
				for (uint i=0; i<8; i++){
					uint pos=positionShuffle(8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, ratio_0, ratioDirection_0);
					if (pos%fft_dim<fft_dim/2)
						sort0[i]=inputs[indexInput(gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, 2*gl_WorkGroupSize.y * gl_WorkGroupID.y + gl_LocalInvocationID.y)];
					else
						sort0[i]=vec2(0,0);
					
					pos=positionShuffle(max_shared_vec2+8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, ratio_0, ratioDirection_0);
					if (pos%fft_dim<fft_dim/2)	
						temp1[i]=inputs[indexInput(gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, 2*gl_WorkGroupSize.y * gl_WorkGroupID.y + gl_LocalInvocationID.y + gl_WorkGroupSize.y)];
					else
						temp1[i]=vec2(0,0);
				
				}

				for (uint i=0; i<8; i++){
					uint pos=positionShuffle(8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, ratio_0, ratioDirection_0);
					
					if (pos < max_shared_vec2)
						sdata[pos]=sort0[i];
						
					pos=positionShuffle(max_shared_vec2+8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, ratio_0, ratioDirection_0);
						
					if (pos < max_shared_vec2)
						sdata[pos]=temp1[i];
				}
				memoryBarrierShared();
				barrier();
		
				for (uint i=0; i<8; i++){
					temp0[i]=sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+i*gl_WorkGroupSize.x];
				}
				memoryBarrierShared();
				barrier();
		
				for (uint i=0; i<8; i++){
					uint pos=positionShuffle(8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, ratio_0, ratioDirection_0);
					
					if (pos >= max_shared_vec2)
						sdata[pos-max_shared_vec2]=sort0[i];
						
					pos=positionShuffle(max_shared_vec2+8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, ratio_0, ratioDirection_0);
						
					if (pos >= max_shared_vec2)
						sdata[pos-max_shared_vec2]=temp1[i];
				}
				memoryBarrierShared();
				barrier();
		
				for (uint i=0; i<8; i++){
					temp1[i]=sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+i*gl_WorkGroupSize.x];
				}
			}else{
				if (gl_LocalInvocationID.y>0) 
					return;
				for (uint i=0; i<4; i++){
					temp0[i]=inputs[indexInput(gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, 2*gl_WorkGroupSize.y * gl_WorkGroupID.y + gl_LocalInvocationID.y)];
					temp1[i]=inputs[indexInput(gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, 2*gl_WorkGroupSize.y * gl_WorkGroupID.y + gl_LocalInvocationID.y + gl_WorkGroupSize.y)];
				}
				for (uint i=4; i<8; i++){
					temp0[i]=vec2(0,0);
					temp1[i]=vec2(0,0);
				
				}
			}
		} else{
			for (uint i=0; i<4; i++){
				temp0[i]=inputs[indexInput(gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, 2*gl_WorkGroupSize.y * gl_WorkGroupID.y + gl_LocalInvocationID.y)];
				temp1[i]=inputs[indexInput(gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, 2*gl_WorkGroupSize.y * gl_WorkGroupID.y + gl_LocalInvocationID.y + gl_WorkGroupSize.y)];
			}
			for (uint i=4; i<8; i++){
				temp0[i]=vec2(0,0);
				temp1[i]=vec2(0,0);
			
			}
		}
	} else {
		if (ratioDirection_0){
			if (gl_WorkGroupID.y < gl_NumWorkGroups.y-1){
				vec2 sort0[8];
				for (uint i=0; i<8; i++){
					sort0[i]=inputs[indexInput(gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, 2*gl_WorkGroupSize.y * gl_WorkGroupID.y + gl_LocalInvocationID.y)];
					temp1[i]=inputs[indexInput(gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, 2*gl_WorkGroupSize.y * gl_WorkGroupID.y + gl_LocalInvocationID.y + gl_WorkGroupSize.y)];
				
				}

				for (uint i=0; i<8; i++){
					uint pos=positionShuffle(8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, ratio_0, ratioDirection_0);
					
					if (pos < max_shared_vec2)
						sdata[pos]=sort0[i];
						
					pos=positionShuffle(max_shared_vec2+8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, ratio_0, ratioDirection_0);
						
					if (pos < max_shared_vec2)
						sdata[pos]=temp1[i];
				}
				memoryBarrierShared();
				barrier();
		
				for (uint i=0; i<8; i++){
					temp0[i]=sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+i*gl_WorkGroupSize.x];
				}
				memoryBarrierShared();
				barrier();
		
				for (uint i=0; i<8; i++){
					uint pos=positionShuffle(8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, ratio_0, ratioDirection_0);
					
					if (pos >= max_shared_vec2)
						sdata[pos-max_shared_vec2]=sort0[i];
						
					pos=positionShuffle(max_shared_vec2+8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, ratio_0, ratioDirection_0);
						
					if (pos >= max_shared_vec2)
						sdata[pos-max_shared_vec2]=temp1[i];
				}
				memoryBarrierShared();
				barrier();
		
				for (uint i=0; i<8; i++){
					temp1[i]=sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+i*gl_WorkGroupSize.x];
				}
			}else{
				if (gl_LocalInvocationID.y>0) 
					return;
				for (uint i=0; i<8; i++){
					temp0[i]=inputs[indexInput(gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, 2*gl_WorkGroupSize.y * gl_WorkGroupID.y + gl_LocalInvocationID.y)];
					temp1[i]=inputs[indexInput(gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, 2*gl_WorkGroupSize.y * gl_WorkGroupID.y + gl_LocalInvocationID.y + gl_WorkGroupSize.y)];
				}
			}
		} else{
			for (uint i=0; i<8; i++){
				temp0[i]=inputs[indexInput(gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, 2*gl_WorkGroupSize.y * gl_WorkGroupID.y + gl_LocalInvocationID.y)];
				temp1[i]=inputs[indexInput(gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, 2*gl_WorkGroupSize.y * gl_WorkGroupID.y + gl_LocalInvocationID.y + gl_WorkGroupSize.y)];
			}
		}

	}
   memoryBarrierShared();
    barrier();
	
	for (uint i=0; i<8; i++){
		sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+i*gl_WorkGroupSize.x]=temp0[i];
	}
	
	memoryBarrierShared();
    barrier();
	
    uint stageSize=1;
    float stageAngle=(inverse) ? -M_PI : M_PI;	
	//0-numStages stage
    for (uint n=0; n < numStages; n++){
		uint current_radix = (n<numStages-1) ? stageRadix_0 : stageRadix_1;
		switch(current_radix){
			case 2:
			{
				uint stageInvocationID = (gl_LocalInvocationID.x) & (stageSize - 1u);
				float angle = stageInvocationID * stageAngle;
				vec2 twiddleFactor = vec2(cos(angle), sin(angle));
				vec2 values[2];
				values[0] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x];
				values[1] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+radixStride_0];
				
				radix2(values, twiddleFactor);
				
				temp0[0]=values[0];
				temp0[4]=values[1];
				
				stageInvocationID = (gl_LocalInvocationID.x+gl_WorkGroupSize.x) & (stageSize - 1u);
				angle = stageInvocationID * stageAngle;
				twiddleFactor = vec2(cos(angle), sin(angle));
				values[0] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+gl_WorkGroupSize.x];
				values[1] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+gl_WorkGroupSize.x+radixStride_0];
				
				radix2(values, twiddleFactor);
				
				temp0[1]=values[0];
				temp0[5]=values[1];
				
				stageInvocationID = (gl_LocalInvocationID.x+2*gl_WorkGroupSize.x) & (stageSize - 1u);
				angle = stageInvocationID * stageAngle;
				twiddleFactor = vec2(cos(angle), sin(angle));
				values[0] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+2*gl_WorkGroupSize.x];
				values[1] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+2*gl_WorkGroupSize.x+radixStride_0];
				
				radix2(values, twiddleFactor);
				
				temp0[2]=values[0];
				temp0[6]=values[1];
				
				stageInvocationID = (gl_LocalInvocationID.x+3*gl_WorkGroupSize.x) & (stageSize - 1u);
				angle = stageInvocationID * stageAngle;
				twiddleFactor = vec2(cos(angle), sin(angle));
				values[0] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+3*gl_WorkGroupSize.x];
				values[1] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+3*gl_WorkGroupSize.x+radixStride_0];
				
				radix2(values, twiddleFactor);
				
				temp0[3]=values[0];
				temp0[7]=values[1];

				break;
			}
			case 4:
			{
				uint stageInvocationID = (gl_LocalInvocationID.x) & (stageSize - 1u);
				float angle = stageInvocationID * stageAngle;
				vec2 twiddleFactor = vec2(cos(angle), sin(angle));
				vec2 values[4];
				values[0] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x];
				values[1] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+radixStride_1];
				values[2] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+2 * radixStride_1];
				values[3] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+3 * radixStride_1];
				
				radix4(values, twiddleFactor);
				
				temp0[0]=values[0];
				temp0[2]=values[1];
				temp0[4]=values[2];
				temp0[6]=values[3];
				
				stageInvocationID = (gl_LocalInvocationID.x+gl_WorkGroupSize.x) & (stageSize - 1u);
				angle = stageInvocationID * stageAngle;
				twiddleFactor = vec2(cos(angle), sin(angle));
				values[0] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+gl_WorkGroupSize.x];
				values[1] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+gl_WorkGroupSize.x+radixStride_1];
				values[2] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+gl_WorkGroupSize.x+2 * radixStride_1];
				values[3] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+gl_WorkGroupSize.x+3 * radixStride_1];
				
				radix4(values, twiddleFactor);
				
				temp0[1]=values[0];
				temp0[3]=values[1];
				temp0[5]=values[2];
				temp0[7]=values[3];
				break;
			}
			case 8:
			{
				uint stageInvocationID = (gl_LocalInvocationID.x) & (stageSize - 1u);
				float angle = stageInvocationID * stageAngle;
				vec2 twiddleFactor = vec2(cos(angle), sin(angle));
				vec2 values[8];
				values[0] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x];
				values[1] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+ radixStride_2];
				values[2] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+2 * radixStride_2];
				values[3] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+3 * radixStride_2];
				values[4] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+4 * radixStride_2];
				values[5] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+5 * radixStride_2];
				values[6] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+6 * radixStride_2];
				values[7] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+7 * radixStride_2];
				
				radix8(values, twiddleFactor);
				
				temp0[0]=values[0];
				temp0[1]=values[1];
				temp0[2]=values[2];
				temp0[3]=values[3];
				temp0[4]=values[4];
				temp0[5]=values[5];
				temp0[6]=values[6];
				temp0[7]=values[7];
				break;
			}
		}
    
        memoryBarrierShared();
        barrier();
		
		switch(current_radix){
			case 2:
			{
				float stageNormalization = (inverse) ? 0.5 : 1.0;
				uint stageInvocationID = (gl_LocalInvocationID.x) & (stageSize - 1u);
				uint blockInvocationID = (gl_LocalInvocationID.x) - stageInvocationID;
				uint outputIndex = stageInvocationID + blockInvocationID * 2;

				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex]=temp0[0]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize]=temp0[4]*stageNormalization;
				
				stageInvocationID = (gl_LocalInvocationID.x+gl_WorkGroupSize.x) & (stageSize - 1u);
				blockInvocationID = (gl_LocalInvocationID.x+gl_WorkGroupSize.x) - stageInvocationID;
				outputIndex = stageInvocationID + blockInvocationID * 2;

				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex]=temp0[1]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize]=temp0[5]*stageNormalization;
				
				stageInvocationID = (gl_LocalInvocationID.x+2*gl_WorkGroupSize.x) & (stageSize - 1u);
				blockInvocationID = (gl_LocalInvocationID.x+2*gl_WorkGroupSize.x) - stageInvocationID;
				outputIndex = stageInvocationID + blockInvocationID * 2;

				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex]=temp0[2]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize]=temp0[6]*stageNormalization;
				
				stageInvocationID = (gl_LocalInvocationID.x+3*gl_WorkGroupSize.x) & (stageSize - 1u);
				blockInvocationID = (gl_LocalInvocationID.x+3*gl_WorkGroupSize.x) - stageInvocationID;
				outputIndex = stageInvocationID + blockInvocationID * 2;

				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex]=temp0[3]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize]=temp0[7]*stageNormalization;
				
				stageSize=stageSize*2;
				stageAngle=stageAngle*0.5f;
				break;
			}
			case 4:
			{
				float stageNormalization = (inverse) ? 0.25 : 1.0;
				uint stageInvocationID = (gl_LocalInvocationID.x) & (stageSize - 1u);
				uint blockInvocationID = (gl_LocalInvocationID.x) - stageInvocationID;
				uint outputIndex = stageInvocationID + blockInvocationID * 4;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex]=temp0[0]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize]=temp0[2]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize*2]=temp0[4]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize*3]=temp0[6]*stageNormalization;
				
				stageInvocationID = (gl_LocalInvocationID.x+gl_WorkGroupSize.x) & (stageSize - 1u);
				blockInvocationID = (gl_LocalInvocationID.x+gl_WorkGroupSize.x) - stageInvocationID;
				outputIndex = stageInvocationID + blockInvocationID * 4;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex]=temp0[1]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize]=temp0[3]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize*2]=temp0[5]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize*3]=temp0[7]*stageNormalization;
				
				stageSize=stageSize*4;
				stageAngle=stageAngle*0.25f;
				break;
			}
			case 8:
			{
				float stageNormalization = (inverse) ? 0.125 : 1.0;
				uint stageInvocationID = (gl_LocalInvocationID.x) & (stageSize - 1u);
				uint blockInvocationID = (gl_LocalInvocationID.x) - stageInvocationID;
				uint outputIndex = stageInvocationID + blockInvocationID * 8;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex]=temp0[0]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize]=temp0[1]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize*2]=temp0[2]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize*3]=temp0[3]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize*4]=temp0[4]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize*5]=temp0[5]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize*6]=temp0[6]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize*7]=temp0[7]*stageNormalization;

				stageSize=stageSize*8;
				stageAngle=stageAngle*0.125f;
				break;
			}
		}

        memoryBarrierShared();
        barrier();
    }
	for (uint i=0; i<8; i++){
		temp0[i]=sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+i*gl_WorkGroupSize.x];
	}
	
    memoryBarrierShared();
    barrier();
	
	for (uint i=0; i<8; i++){
		sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+i*gl_WorkGroupSize.x]=temp1[i];
	}
	
	memoryBarrierShared();
    barrier();
	
    stageSize=1;
    stageAngle=(inverse) ? -M_PI : M_PI;	
	//0-numStages stage
    for (uint n=0; n < numStages; n++){
		uint current_radix = (n<numStages-1) ? stageRadix_0 : stageRadix_1;
		switch(current_radix){
			case 2:
			{
				uint stageInvocationID = (gl_LocalInvocationID.x) & (stageSize - 1u);
				float angle = stageInvocationID * stageAngle;
				vec2 twiddleFactor = vec2(cos(angle), sin(angle));
				vec2 values[2];
				values[0] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x];
				values[1] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+radixStride_0];
				
				radix2(values, twiddleFactor);
				
				temp1[0]=values[0];
				temp1[4]=values[1];
				
				stageInvocationID = (gl_LocalInvocationID.x+gl_WorkGroupSize.x) & (stageSize - 1u);
				angle = stageInvocationID * stageAngle;
				twiddleFactor = vec2(cos(angle), sin(angle));
				values[0] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+gl_WorkGroupSize.x];
				values[1] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+gl_WorkGroupSize.x+radixStride_0];
				
				radix2(values, twiddleFactor);
				
				temp1[1]=values[0];
				temp1[5]=values[1];
				
				stageInvocationID = (gl_LocalInvocationID.x+2*gl_WorkGroupSize.x) & (stageSize - 1u);
				angle = stageInvocationID * stageAngle;
				twiddleFactor = vec2(cos(angle), sin(angle));
				values[0] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+2*gl_WorkGroupSize.x];
				values[1] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+2*gl_WorkGroupSize.x+radixStride_0];
				
				radix2(values, twiddleFactor);
				
				temp1[2]=values[0];
				temp1[6]=values[1];
				
				stageInvocationID = (gl_LocalInvocationID.x+3*gl_WorkGroupSize.x) & (stageSize - 1u);
				angle = stageInvocationID * stageAngle;
				twiddleFactor = vec2(cos(angle), sin(angle));
				values[0] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+3*gl_WorkGroupSize.x];
				values[1] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+3*gl_WorkGroupSize.x+radixStride_0];
				
				radix2(values, twiddleFactor);
				
				temp1[3]=values[0];
				temp1[7]=values[1];

				break;
			}
			case 4:
			{
				uint stageInvocationID = (gl_LocalInvocationID.x) & (stageSize - 1u);
				float angle = stageInvocationID * stageAngle;
				vec2 twiddleFactor = vec2(cos(angle), sin(angle));
				vec2 values[4];
				values[0] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x];
				values[1] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+radixStride_1];
				values[2] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+2 * radixStride_1];
				values[3] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+3 * radixStride_1];
				
				radix4(values, twiddleFactor);
				
				temp1[0]=values[0];
				temp1[2]=values[1];
				temp1[4]=values[2];
				temp1[6]=values[3];
				
				stageInvocationID = (gl_LocalInvocationID.x+gl_WorkGroupSize.x) & (stageSize - 1u);
				angle = stageInvocationID * stageAngle;
				twiddleFactor = vec2(cos(angle), sin(angle));
				values[0] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+gl_WorkGroupSize.x];
				values[1] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+gl_WorkGroupSize.x+radixStride_1];
				values[2] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+gl_WorkGroupSize.x+2 * radixStride_1];
				values[3] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+gl_WorkGroupSize.x+3 * radixStride_1];
				
				radix4(values, twiddleFactor);
				
				temp1[1]=values[0];
				temp1[3]=values[1];
				temp1[5]=values[2];
				temp1[7]=values[3];
				break;
			}
			case 8:
			{
				uint stageInvocationID = (gl_LocalInvocationID.x) & (stageSize - 1u);
				float angle = stageInvocationID * stageAngle;
				vec2 twiddleFactor = vec2(cos(angle), sin(angle));
				vec2 values[8];
				values[0] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x];
				values[1] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+ radixStride_2];
				values[2] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+2 * radixStride_2];
				values[3] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+3 * radixStride_2];
				values[4] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+4 * radixStride_2];
				values[5] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+5 * radixStride_2];
				values[6] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+6 * radixStride_2];
				values[7] = sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+7 * radixStride_2];
				
				radix8(values, twiddleFactor);
				
				temp1[0]=values[0];
				temp1[1]=values[1];
				temp1[2]=values[2];
				temp1[3]=values[3];
				temp1[4]=values[4];
				temp1[5]=values[5];
				temp1[6]=values[6];
				temp1[7]=values[7];
				break;
			}
		}
    
        memoryBarrierShared();
        barrier();
		
		switch(current_radix){
			case 2:
			{
				float stageNormalization = (inverse) ? 0.5 : 1.0;
				uint stageInvocationID = (gl_LocalInvocationID.x) & (stageSize - 1u);
				uint blockInvocationID = (gl_LocalInvocationID.x) - stageInvocationID;
				uint outputIndex = stageInvocationID + blockInvocationID * 2;

				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex]=temp1[0]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize]=temp1[4]*stageNormalization;
				
				stageInvocationID = (gl_LocalInvocationID.x+gl_WorkGroupSize.x) & (stageSize - 1u);
				blockInvocationID = (gl_LocalInvocationID.x+gl_WorkGroupSize.x) - stageInvocationID;
				outputIndex = stageInvocationID + blockInvocationID * 2;

				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex]=temp1[1]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize]=temp1[5]*stageNormalization;
				
				stageInvocationID = (gl_LocalInvocationID.x+2*gl_WorkGroupSize.x) & (stageSize - 1u);
				blockInvocationID = (gl_LocalInvocationID.x+2*gl_WorkGroupSize.x) - stageInvocationID;
				outputIndex = stageInvocationID + blockInvocationID * 2;

				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex]=temp1[2]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize]=temp1[6]*stageNormalization;
				
				stageInvocationID = (gl_LocalInvocationID.x+3*gl_WorkGroupSize.x) & (stageSize - 1u);
				blockInvocationID = (gl_LocalInvocationID.x+3*gl_WorkGroupSize.x) - stageInvocationID;
				outputIndex = stageInvocationID + blockInvocationID * 2;

				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex]=temp1[3]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize]=temp1[7]*stageNormalization;
				
				stageSize=stageSize*2;
				stageAngle=stageAngle*0.5f;
				break;
			}
			case 4:
			{
				float stageNormalization = (inverse) ? 0.25 : 1.0;
				uint stageInvocationID = (gl_LocalInvocationID.x) & (stageSize - 1u);
				uint blockInvocationID = (gl_LocalInvocationID.x) - stageInvocationID;
				uint outputIndex = stageInvocationID + blockInvocationID * 4;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex]=temp1[0]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize]=temp1[2]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize*2]=temp1[4]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize*3]=temp1[6]*stageNormalization;
				
				stageInvocationID = (gl_LocalInvocationID.x+gl_WorkGroupSize.x) & (stageSize - 1u);
				blockInvocationID = (gl_LocalInvocationID.x+gl_WorkGroupSize.x) - stageInvocationID;
				outputIndex = stageInvocationID + blockInvocationID * 4;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex]=temp1[1]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize]=temp1[3]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize*2]=temp1[5]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize*3]=temp1[7]*stageNormalization;
				
				stageSize=stageSize*4;
				stageAngle=stageAngle*0.25f;
				break;
			}
			case 8:
			{
				float stageNormalization = (inverse) ? 0.125 : 1.0;
				uint stageInvocationID = (gl_LocalInvocationID.x) & (stageSize - 1u);
				uint blockInvocationID = (gl_LocalInvocationID.x) - stageInvocationID;
				uint outputIndex = stageInvocationID + blockInvocationID * 8;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex]=temp1[0]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize]=temp1[1]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize*2]=temp1[2]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize*3]=temp1[3]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize*4]=temp1[4]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize*5]=temp1[5]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize*6]=temp1[6]*stageNormalization;
				sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + outputIndex+stageSize*7]=temp1[7]*stageNormalization;

				stageSize=stageSize*8;
				stageAngle=stageAngle*0.125f;
				break;
			}
		}

        memoryBarrierShared();
        barrier();
    }
	
	for (uint i=0; i<8; i++){
		temp1[i]=sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+i*gl_WorkGroupSize.x];
	}
	memoryBarrierShared();
	barrier();
	
	
	if (ratioDirection_1){
		for (uint i=0; i<8; i++)
			outputs[indexOutput(gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, 2*gl_WorkGroupSize.y * gl_WorkGroupID.y + gl_LocalInvocationID.y)]=temp0[i];
		for (uint i=0; i<8; i++)
			outputs[indexOutput(gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, 2*gl_WorkGroupSize.y * gl_WorkGroupID.y + gl_LocalInvocationID.y + gl_WorkGroupSize.y)]=temp1[i];
		
	}else{
		for (uint i=0; i<8; i++){
			uint pos=positionShuffle(8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, ratio_0, ratioDirection_0);
			
			if (pos < max_shared_vec2)
				sdata[pos]=temp0[i];
				
			pos=positionShuffle(max_shared_vec2+8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, ratio_0, ratioDirection_0);
				
			if (pos < max_shared_vec2)
				sdata[pos]=temp1[i];
		}
		memoryBarrierShared();
		barrier();
		for (uint i=0; i<8; i++){
			outputs[indexOutput(gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, 2*gl_WorkGroupSize.y * gl_WorkGroupID.y + gl_LocalInvocationID.y)]=sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+i*gl_WorkGroupSize.x];
		}
		memoryBarrierShared();
		barrier();
		for (uint i=0; i<8; i++){
			uint pos=positionShuffle(8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, ratio_0, ratioDirection_0);
			
			if (pos >= max_shared_vec2)
				sdata[pos-max_shared_vec2]=temp0[i];
				
			pos=positionShuffle(max_shared_vec2+8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, ratio_0, ratioDirection_0);
				
			if (pos >= max_shared_vec2)
				sdata[pos-max_shared_vec2]=temp1[i];
		}
		memoryBarrierShared();
		barrier();
		for (uint i=0; i<8; i++){
			outputs[indexOutput(gl_LocalInvocationID.x+i*gl_WorkGroupSize.x, 2*gl_WorkGroupSize.y * gl_WorkGroupID.y + gl_LocalInvocationID.y)]=sdata[8*gl_WorkGroupSize.x*gl_LocalInvocationID.y + gl_LocalInvocationID.x+i*gl_WorkGroupSize.x];
		}
	}
	
}
